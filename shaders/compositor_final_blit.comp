#version 450

#include "global_binding.glsl"
#include "math.glsl"
#include "subgroup_grid.glsl"
#include "logging.glsl"

#define BIND_ARRAY_INDEX_FINAL_BLIT_SRC_GRAPHICS_FRAMEBUFFER 0
#define BIND_ARRAY_INDEX_FINAL_BLIT_SRC_COMPUTE_FRAMEBUFFER 1

layout (local_size_x = SUBGROUP_COUNT, local_size_y = WORKGROUP_SUBGROUP_COUNT, local_size_z = 1) in;

layout (set = 1, binding = 0, rgba8) uniform readonly image2D src[];
layout (set = 1, binding = 1, rgba8) uniform writeonly image2D dst;

#define STEPS 32
#define DIST 0.01

vec3 WorldRayDirFromNDC(vec2 ndc, mat4 invProj, mat4 invView)
{
    vec4 clipRayDir = vec4(ndc, 0, 1);
    // this is actrually onl faster than WorldRayDirFromNDC2 if somehow we can do one mult with invViewProj
    vec4 viewSpace = invProj * clipRayDir;
    vec4 viewDir = vec4(viewSpace.xy, -1, 0); // we look down -1 z in view space for vulkan?
    vec3 worldRayDir = normalize((invView * viewDir).xyz);
    return worldRayDir;
}

vec3 WorldRayDirFromNDC2(vec2 ndc, mat4 invViewProj)
{
    vec4 clipNear = vec4(ndc, 1, 1);
    vec4 clipFar = vec4(ndc, 0, 1);

    vec4 worldPosNear = invViewProj * clipNear;
    worldPosNear.xyz /= worldPosNear.w;

    vec4 worldPosFar = invViewProj * clipFar;
    worldPosFar.xyz /= worldPosFar.w;

    vec3 nearToFarRay = worldPosFar.xyz - worldPosNear.xyz;
    return normalize(nearToFarRay);
}

vec2 NDCFromUV(vec2 uv) { return uv * 2.0f - 1.0f; }

float DDATrace(vec3 rayOrigin, vec3 rayDirNormalized) {

    const int stepCount = 16;

    vec3 rayDirInv = 1.0f / rayDirNormalized;
    vec3 rayDirSign = sign(rayDirNormalized);

    vec3 deltaDist = abs(rayDirInv);
    ivec3 deltaVoxel = ivec3(rayDirSign);

    vec3 fractPart = rayOrigin - floor(rayOrigin);
    vec3 sideDist = (rayDirSign * (0.5 - fractPart) + 0.5) * deltaDist;

    ivec3 voxel = ivec3(floor(rayOrigin));
    float color = 0;

    // Technically this background could be rendered at lower rate in the UI/Shell thread/process
    for (int i = 0; i < stepCount; i++) {

        bvec3 mask = lessThanEqual(sideDist.xyz, min(sideDist.yzx, sideDist.zxy));

        sideDist += vec3(mask) * deltaDist;
        voxel += ivec3(mask) * deltaVoxel;

        bvec3 isNegativeDelta = lessThan(deltaVoxel, ivec3(0.0));
        ivec3 hitVoxelSide = voxel + ivec3(isNegativeDelta); // + 1 if left/negative side
        vec3 hitRay = (hitVoxelSide - rayOrigin) / rayDirNormalized;
        float t = mask.x ? hitRay.x : mask.y ? hitRay.y : mask.z ? hitRay.z : 0;

        vec3 hitPos = rayOrigin + rayDirNormalized * t;
        float hitPosDistance = distance(rayOrigin, hitPos);
        float hitPosDistanceFade = linearStep(stepCount, 0.0, hitPosDistance);
        float nearFade = clamp(hitPosDistance * hitPosDistance - 0.5, 0.0, 1.0);

        vec3 localPos = hitPos - voxel;
        vec2 local2DPos = mask.x ? localPos.yz : mask.y ? localPos.xz : mask.z ? localPos.xy : vec2(0.5);
        vec2 local2DPosCenter = abs(local2DPos - 0.5) * 2.0;

        float local2DPosCenterBoxDistance = max(local2DPosCenter.x, local2DPosCenter.y);
        float local2DPosCenterCircleDistance = length(local2DPosCenter);
        float local2DPosCenterCornerDistance = 1.0 - length(1.0 - local2DPosCenter);

        float threshold = 0.002 * hitPosDistance;
        float circleEdgeSubtract = (1.0 - local2DPosCenterCornerDistance);
        float edge = smoothstep(1.0 - threshold, 1.0, local2DPosCenterBoxDistance) - circleEdgeSubtract;

        float circles = smoothstep(.98 - threshold, .99, local2DPosCenterCornerDistance);
        float cornerEdgeCircles = circles + (edge * 0.2);

        color = max(color, cornerEdgeCircles * hitPosDistanceFade * nearFade);
    }

    return color;
}

void main()
{
    ivec2 outputSize = imageSize(dst);
    InitializeSubgroupGridInfo(outputSize);

    vec2 ndc = NDCFromUV(grid_GlobalUV);
    vec3 camDir = WorldRayDirFromNDC(ndc,  globalUBO.invProj,  globalUBO.invView);
    vec3 camPos = (globalUBO.invView * vec4(0,0,0,1)).xyz;

    float result = DDATrace(camPos, camDir);
    vec4 traceColor = vec4(result,result,result,1.0);

    vec4 graphicsSample = imageLoad(src[BIND_ARRAY_INDEX_FINAL_BLIT_SRC_GRAPHICS_FRAMEBUFFER], grid_GlobalCoord);
    vec4 computeSample = imageLoad(src[BIND_ARRAY_INDEX_FINAL_BLIT_SRC_COMPUTE_FRAMEBUFFER], grid_GlobalCoord);
    vec4 finalColor = computeSample.a > 0 ? computeSample : graphicsSample.a > 0 ? graphicsSample : traceColor;
    imageStore(dst, grid_GlobalCoord, finalColor);
}

